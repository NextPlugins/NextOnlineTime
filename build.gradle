plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.2.1'

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {

    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'

    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'de.tr7zw:item-nbt-api:2.8.0'

    implementation "com.google.inject:guice:4.0"
    implementation "javax.inject:javax.inject:1"
    implementation "aopalliance:aopalliance:1.0"
    implementation "org.ow2.asm:asm:8.0.1"

    def lombok = "org.projectlombok:lombok:1.18.16"
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.onlinetime.NextOnlineTime'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    apiVersion = '1.12.2'
    load = 'POSTWORLD'
    softDepend = ['PlaceholderAPI', 'Citizens', 'HolographicDisplays']

    commands {
        tempo {
            aliases = ['tempoonline']
        }
    }
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.onlinetime.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.onlinetime.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.onlinetime.libs.hikari'

    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.onlinetime.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.nextplugins.onlinetime.libs.annotations'

    relocate 'com.google.common', 'com.nextplugins.onlinetime.libs.guice.common'
    relocate 'com.google.inject', 'com.nextplugins.onlinetime.libs.guice.inject'
    relocate 'com.google.thirdparty', 'com.nextplugins.onlinetime.libs.guice.thirdparty'
    relocate 'javax.inject', 'com.nextplugins.onlinetime.libs.guice.inject.javax'
}

compileJava {
    options.encoding = "UTF-8"

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
